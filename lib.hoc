
// Dictionary template
{xopen("./templates/Dict.hoc")}

proc assert() { local test strdef errmsg
    
    test = $1
    errmsg = $s2
    
    if (!test) {
        execerror ("Assertion failed: ", errmsg)
    }
}

// Writes out connectivity graph
proc graphout() { local i,j,node localobj f, v, conlist
    f = new File($s1)
    conlist = $o2
    f.wopen()
    for i=0, conlist.count()-1 {
        v = conlist.object(i)
        node = v.x[0]
        for j=1, v.size()-1 {
            f.printf("%d %d\n", node, v.x[j])
        }
    }
    f.close()
}

// Writes out spike time information
proc spikeout() { local i localobj f, spikevec, idvec
    
    f = new File($s1)
    spikevec = $o2
    idvec = $o3
    
    f.wopen()
    for i=0, spikevec.size-1 {
	f.printf("%g %d\n", spikevec.x[i], idvec.x[i])
    }
    f.close()
}

// Returns the index of the named cell type or -1 if the type was not found
func findCellType() { local i, result localobj cellTypes, cellType
    
    strdef typeName
    
    cellTypes = $o1
    typeName = $s2
    
    
    result = -1
    
    for i=0, cellTypes.count()-1 {
        
        cellType = cellTypes.o(i)
        if (strcmp(typeName, cellType.getPropertyString("typeName").s) == 0) {
            result = i
            break
        }
        
    }
    
    return result
}

// Loads cell type info into a Dict
proc loadCellCategoryInfo() { local i, numCells, offset localobj f, cellTypes, cellType
    
    strdef cellTypeName, cellTypeTemplateName
    
    // The first argument must be a list
    cellTypes = $o1
    
    // The second argument must be the path to the cell types file 
    f = new File()
    f.ropen($s2)
    
    // Scan the first line, which contains the number of cell types
    numCellTypes = f.scanvar()
    
    offset = 0
    for i=0, numCellTypes-1 {
        
        // Scan in the cell name, template name, number of cells
	f.scanstr(cellTypeName)
	f.scanstr(cellTypeTemplateName)
	numCells = f.scanvar()
        cellType = new Dict(cellTypeName)
        cellType.addPropertyString("typeName",cellTypeName)
        cellType.addPropertyString("templateName",cellTypeTemplateName)
        cellType.addPropertyScalar("numCells",numCells)
        cellType.addPropertyScalar("offset",offset)
        cellTypes.append(cellType)
        offset = offset + numCells
    }
    f.close()
    
}

// Loads connectivity info into a Dict
proc loadConnectivityInfo() { local i, order, synIndex, wdType, standardWeight, standardVelocity, numConnectivityTypes localobj f, connectivityTypes, connectivityType
    
    strdef connectivityName, preSynapticPopulation, postSynapticPopulation
    
    // The first argument must be a list
    connectivityTypes = $o1
    
    // The second argument must be the path to the main connection file 
    f = new File()
    f.ropen($s2)
    
    // Scan the first line, which contains the number of connectivity matrices
    numConnectivityTypes = f.scanvar()
    
    for i=0, numConnectivityTypes-1 {
        
        // Read in the connectivity cell id numbering (0 - relative, 1 - absolute)
	order = f.scanvar()
        // Read in the connectivity name
	f.scanstr(connectivityName)
        // Read in the name of the presynaptic population
	f.scanstr(preSynapticPopulation)
        // Read in the name of the postsynaptic population
	f.scanstr(postSynapticPopulation)
        // Read in the post-synaptic index (-1 for artificial cells, -2 random selection)
	synIndex = f.scanvar()
        // Read in the type of weight+delay specification
	wdType = f.scanvar()
        if (wdType == 1) {
            connectivityType = new Dict(connectivityName)
            connectivityType.addPropertyString("name",connectivityName)
            connectivityType.addPropertyString("presynapticPopulation",preSynapticPopulation)
            connectivityType.addPropertyString("postsynapticPopulation",postSynapticPopulation)
            connectivityType.addPropertyScalar("synIndex",synIndex)
            connectivityType.addPropertyScalar("wdType",wdType)
            connectivityType.addPropertyScalar("order",order)
            connectivityTypes.append(connectivityType)
        } else if (wdType == 2) {
            standardWeight = f.scanvar()
            standardVelocity = f.scanvar()
            connectivityType = new Dict(connectivityName)
            connectivityType.addPropertyString("name",connectivityName)
            connectivityType.addPropertyString("presynapticPopulation",preSynapticPopulation)
            connectivityType.addPropertyString("postsynapticPopulation",postSynapticPopulation)
            connectivityType.addPropertyScalar("synIndex",synIndex)
            connectivityType.addPropertyScalar("wdType",wdType)
            connectivityType.addPropertyScalar("standardWeight",standardWeight)
            connectivityType.addPropertyScalar("standardVelocity",standardVelocity)
            connectivityType.addPropertyScalar("order",order)
            connectivityTypes.append(connectivityType)
        }
    }
    f.close()
    
}

proc loadStimulationParameters() { localobj f, m strdef stimulationPath
    
    m = $o1
    stimulationPath = $s2
    f = new File(stimulationPath)
    f.ropen()
    m.scanf(f)
    f.close()
    
}